/*
 * Copyright (C) 2018 Knot.x Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
defaultTasks = ['build']

allprojects {
  group = 'io.knotx.databridge'
  version = '1.3.0-SNAPSHOT'

  apply plugin: "eclipse"
  apply plugin: "idea"

  repositories {
    maven { url "https://oss.sonatype.org/content/groups/staging/" }
    maven { url "https://repo1.maven.org/maven2" }
    jcenter()
  }
}

subprojects { subproject ->

  plugins.withId 'java-library', {

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def compileOptions = {
      options.encoding = "UTF-8"
      options.compilerArgs << '-Xlint:deprecation'
      options.incremental = true
    }

    compileJava compileOptions
    compileTestJava compileOptions

    dependencies {
      // we can not use 'implementation' because of the annotationProcessing task configuration
      compile group: 'io.knotx', name: 'knotx-dependencies', version: '1.3.0-SNAPSHOT'

      compile group: 'io.vertx', name: 'vertx-codegen'
      compile group: 'io.knotx', name: 'knotx-core'
      testCompile group: 'io.vertx', name: 'vertx-unit'
      testCompile group: 'io.knotx', name: 'knotx-core', classifier: 'tests'
      testCompile group: 'io.knotx', name: 'knotx-mocks'

      // BOM dependencies are not available in test scope
      testCompile group: 'junit', name: 'junit', version: '4.12'
      testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.6'
      testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.6'
      testCompile group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'
    }
  }

}
