/*
 * Copyright (C) 2018 Knot.x Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
  id "io.spring.dependency-management" version "1.0.5.RELEASE" apply false
}

ext {
  knotxVersion = '1.3.0-SNAPSHOT'
  wiremockVersion = '2.16.0'
}

defaultTasks = ['build']

allprojects {
  group = 'io.knotx'
  version = '1.3.0-SNAPSHOT'

  apply plugin: "eclipse"
  apply plugin: "idea"
  apply plugin: 'maven'

  repositories {
    maven { url "https://oss.sonatype.org/content/groups/staging/" }
    maven { url "https://repo1.maven.org/maven2" }
    jcenter()
  }
}

subprojects { subproject ->

  plugins.withId 'java-library', {

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def compileOptions = {
      options.encoding = "UTF-8"
      options.compilerArgs << '-Xlint:deprecation'
      options.incremental = true
    }

    compileJava compileOptions
    compileTestJava compileOptions

    jar {
      manifest.attributes["Implementation-Title"] = subproject.name
      manifest.attributes["Implementation-Version"] = subproject.version
      manifest.attributes["Automatic-Module-Name"] = subproject.name.replace('-', '.')  // for Jigsaw

      from("${rootProject.projectDir}") {
        include "LICENSE"
        into "META-INF"
        expand(copyright: new Date().format("yyyy"), version: project.version)
      }
    }
  }

  plugins.withId 'maven', {

    // http://central.sonatype.org/pages/gradle.html

    //signing {
    //  sign configurations.archives
    //}

    uploadArchives {
      repositories {
        mavenDeployer {
        // beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }

          snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }

          pom.project {
            name 'Knotx Data Bridge API'
            packaging 'jar'
            description 'Knotx Data Bridge API allowing to handle different data sources.'
            url 'http://knotx.io'

            scm {
              connection 'scm:git:git://github.com/Knotx/knotx-data-bridge.git'
              developerConnection 'scm:git:git://github.com/Knotx/knotx-data-bridge.git'
              url 'http://github.com/Knotx/knotx-data-bridge/tree/master'
            }

            licenses {
              license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              }
            }
          }
        }
      }
    }
  }

}
